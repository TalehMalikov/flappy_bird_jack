class Game{
    field Bird bird;
    field Pipe pipe;
    field boolean isGameOver;
    field int score;

    constructor Game new() {
        let bird = Bird.new(32, 128);
        let pipe = Pipe.new(400, 80, 150);
        let isGameOver = false;
        let score = 0;
        do Random.setSeed(12345);
        return this;
    }

    method boolean checkGameOver() {
        if (isGameOver) {
            return true;
        }
        if (bird.getY() < 1) {
            let isGameOver = true;
            return true;
        }
        if (bird.getY() > 220) {
            let isGameOver = true;
            return true;
        }
        if (pipe.collidesWith(bird)) {
            let isGameOver = true;
            return true;
        }
        return false;
    }

    method void run() {
        var char key;

        while (~checkGameOver()) {
            let key = Keyboard.keyPressed();
            if (key = 32) {
                do bird.flap();
            }

            do bird.erase();
            do pipe.erase();

            do bird.move();
            do pipe.move();

            if (pipe.isOffScreen()) {
                do pipe.erase();
                do pipe.dispose();
                let pipe = Pipe.new(511, Random.randRange(30, 120), 150);
                let score = score + 1;
            }

            do bird.draw();
            do pipe.draw();

            do Output.moveCursor(0, 0);
            do Output.printInt(score);
            do Sys.wait(30);
        }

        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString("Game Over! Score: ");
        do Output.printInt(score);
        return;
    }

    method void dispose() {
        do bird.dispose();
        do pipe.dispose();
        do Memory.deAlloc(this);
        return;
    }
}