class Pipe{
    field int x;
    field int gapTop;
    field int gapSize;
    
    constructor Pipe new(int ax, int agapTop, int agapSize) {
        let x = ax;
        let gapTop = agapTop;
        let gapSize = agapSize;
        return this;
    }

    method void move() {
        let x = x - 3;
        return;
    }

    method boolean isOffScreen() {
        if (x < 16) {
            return true;
        }
        return false;
    }

    method void draw() {
        if (x < 0) { return; }
        if ((x + 32) > 511) { return; }
        do Screen.setColor(true);
        if (gapTop > 0) {
            do Screen.drawRectangle(x, 0, x+32, gapTop);
        }
        if ((gapTop + gapSize) < 255) {
            do Screen.drawRectangle(x, gapTop+gapSize, x+32, 255);
        }
        return;
    }

    method void erase() {
        if (x < 0) { return; }
        if ((x + 32) > 511) { return; }
        do Screen.setColor(false);
        if (gapTop > 0) {
            do Screen.drawRectangle(x, 0, x+32, gapTop);
        }
        if ((gapTop + gapSize) < 255) {
            do Screen.drawRectangle(x, gapTop+gapSize, x+32, 255);
        }
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method boolean collidesWith(Bird bird) {
        var int birdX;
        var int birdY;
        let birdX = bird.getX();
        let birdY = bird.getY();
        
        if ((birdX + 32) > x) {
            if (birdX < (x + 32)) {
                if (birdY < gapTop) {
                    return true;
                }
                if ((birdY + 16) > (gapTop + gapSize)) {
                    return true;
                }
            }
        }
        return false;
    }
}